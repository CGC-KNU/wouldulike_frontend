pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }()
    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")
    
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    
    // Kotlin 버전 강제 설정
    resolutionStrategy {
        eachPlugin {
            if (requested.id.namespace == 'org.jetbrains.kotlin') {
                useVersion '1.9.20'
            }
        }
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.1.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.20" apply false
    id "com.google.gms.google-services" version "4.4.2" apply false
}

include ":app"

// Ensure all project dependencies resolve from these repositories
dependencyResolutionManagement {
    // Prefer project-defined repositories so Flutter can add its local maven repo
    repositoriesMode.set(RepositoriesMode.PREFER_PROJECT)
    repositories {
        google()
        mavenCentral()
        // Fallback for some Kakao artifacts if needed
        maven { url "https://devrepo.kakao.com/nexus/content/groups/public/" }
    }
}